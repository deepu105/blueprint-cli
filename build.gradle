plugins {
    id 'com.github.blindpirate.gogradle' version '0.10'
}

apply plugin: 'maven-publish'

golang {
    packagePath = 'github.com/xebialabs/xl-cli' // go import path of project to be built, NOT local file system path!
}

dependencies {
    golang {
        build(['name': 'github.com/mitchellh/go-homedir'])
        build(['name': 'github.com/spf13/cobra'])
        build(['name': 'github.com/spf13/viper'])
    }
}

repositories {
    mavenCentral()
}

build {
    // Cross-compile platforms, update as necessary.
    targetPlatform = [
            'darwin-amd64', 'linux-amd64', 'windows-amd64'
    ]

    // The ${} placeholder will be rendered in cross-compile
    go 'build -o ./build/${GOOS}-${GOARCH}/xl${GOEXE} cmd/xl/main.go'
}

clean.doFirst {
    delete 'build'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.xebialabs.xlclient'
            artifactId 'xl-client'
            version '8.2.0-alpha.2'
            artifact("./build/darwin-amd64/xl") {
                classifier = 'darwin-amd64'
                extension = 'bin'
            }
            artifact("./build/linux-amd64/xl") {
                classifier = 'linux-amd64'
                extension = 'bin'
            }
            artifact("./build/windows-amd64/xl.exe") {
                classifier = 'windows-amd64'
                extension = 'exe'
            }
        }
    }
    repositories {
        maven {
            url "${nexusBaseUrl}/repositories/alphas/"
            credentials {
                username nexusUserName
                password nexusPassword
            }
        }
    }
}