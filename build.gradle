import com.github.blindpirate.gogradle.Go

import java.nio.file.Paths
import java.text.SimpleDateFormat
import jp.classmethod.aws.gradle.s3.AmazonS3FileUploadTask

buildscript {
    repositories {
        mavenLocal()
        ["public", "thirdparty", "releases"].each { r ->
            maven {
                credentials {
                    username nexusUserName
                    password nexusPassword
                }
                url "${nexusBaseUrl}/repositories/${r}"
            }
        }
    }

    dependencies {
        classpath "com.xebialabs.gradle.plugins:gradle-xl-plugins-plugin:${xlPluginsPluginVersion}"
    }
}

plugins {
    id 'com.github.blindpirate.gogradle' version '0.11.3'
    id "jp.classmethod.aws" version "0.38"
    id 'org.sonarqube' version '2.6.2'
}

apply plugin: 'xebialabs.root.opinions'
apply plugin: 'xebialabs.opinions'
apply plugin: 'maven-publish'
apply plugin: 'jp.classmethod.aws.s3'
apply plugin: 'org.sonarqube'

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.xebialabs.xlclient'
            artifactId 'xl-client'
            artifact("./build/darwin-amd64/xl") {
                classifier = 'darwin-amd64'
                extension = 'bin'
            }
            artifact("./build/linux-amd64/xl") {
                classifier = 'linux-amd64'
                extension = 'bin'
            }
            artifact("./build/windows-amd64/xl.exe") {
                classifier = 'windows-amd64'
                extension = 'exe'
            }
        }
    }

    repositories {
        maven {
            def alphasRepoUrl = "${nexusBaseUrl}/repositories/alphas/"
            def releasesRepoUrl = "${nexusBaseUrl}/repositories/releases/"
            url project.version.toString().contains('alpha') ? alphasRepoUrl : releasesRepoUrl
            credentials {
                username nexusUserName
                password nexusPassword
            }
        }
    }
}

golang {
    goVersion = '1.11'
    packagePath = 'github.com/xebialabs/xl-cli' // go import path of project to be built, NOT local file system path!
}

dependencies {
    golang {
        // *** WARNING *** since we now have license information embedded in the application
        // You need to do some additional steps when adding a library. See README for more details
        // *** NOTICE **** always verify the information from the tool since its based on heuristics
        build name: 'github.com/gobuffalo/packr',           tag: 'v2.0.1'
        build name: 'github.com/spf13/cobra',               tag: 'v0.0.3'
        build name: 'github.com/spf13/viper',               tag: 'v1.1.0'
        build name: 'github.com/mitchellh/go-homedir',      tag: 'v1.0.0'
        build name: 'github.com/xebialabs/yaml',            branch: 'v2'
        build name: 'github.com/jhoonb/archivex/archivex'
        build name: 'github.com/mholt/archiver',            tag: 'v2.1.0'
        build name: 'github.com/olekukonko/tablewriter',    tag: 'v0.0.1'
        build name: 'github.com/Masterminds/sprig',         tag: 'v2.16.0'
        build name: 'gopkg.in/AlecAivazis/survey.v1',       tag: 'v1.8.0'
        build name: 'github.com/aws/aws-sdk-go',            tag: 'v1.15.45'
        build name: 'github.com/huandu/xstrings',           tag: 'v1.2.0'
        build name: 'github.com/deckarep/golang-set',       tag: 'v1.7.1'
        build name: 'github.com/magiconair/properties',     tag: 'v1.8.0'
        build name: 'github.com/thoas/go-funk',             tag: '0.4'
        build name: 'github.com/google/go-github',          tag: 'v21.0.0'
        build name: 'github.com/disiqueira/gotree',         tag: '2.0.3'
        build name: 'github.com/briandowns/spinner',        tag: '1.4'
        build name: 'github.com/fatih/color',               tag: 'v1.7.0'
        build name: 'github.com/Knetic/govaluate',          tag: '3.0.0'
        build name: 'github.com/mattn/go-isatty',           tag: 'v0.0.6'
        test name: 'github.com/stretchr/testify/',          tag: 'v1.2.2'
        test name: 'github.com/stretchr/testify/assert',    tag: 'v1.2.2'
        test name: 'golang.org/x/crypto/ssh/terminal'
        test name: 'github.com/jarcoal/httpmock',           tag: 'v1'
    }
}

task installTemplify(type: Go) {
    go "get github.com/wlbr/templify"
}

task installPackr(type: Go) {
    go "get github.com/gobuffalo/packr/packr"
}

task installGlice(type: Go) {
    go "get github.com/ribice/glice"
}

installDependencies.dependsOn installTemplify, installPackr

goBuild {
    // Cross-compile platforms, update as necessary.
    targetPlatform = [
        'darwin-amd64', 'linux-amd64', 'windows-amd64'
    ]

    environment "CGO_ENABLED", "0" // To make sure a fully statically linked binary is produced even when building on linux

    def gitCommit = runCmd('git', 'rev-parse', 'HEAD')
    def gitVersion = runCmd('git', 'describe', '--long', '--dirty')
    def gitVersionShort = gitVersion.startsWith("xl-client-") ? gitVersion.substring(10) : gitVersion

    def simpleDateFormat = new SimpleDateFormat("yyy-MM-dd'T'HH:mm:ss.SSS'Z'")
    simpleDateFormat.setTimeZone(TimeZone.getTimeZone("UTC"))
    def date = simpleDateFormat.format(new Date())

    def ldflags = " -ldflags='" +
        ldflag("CliVersion", { -> project.version}) +
        ldflag("BuildVersion", gitVersionShort) +
        ldflag("BuildGitCommit", gitCommit) +
        ldflag("BuildDate", date) +
        (project.hasProperty("optimise") ? "-s -w" : "") +
        "'"

    def debugFlags = project.hasProperty("debug") ? " -gcflags \"all=-N -l\"" : ""

    run Paths.get("\${GOPATH}/bin/packr").toString() + " -i cmd/xl"
    go "generate cmd/xl/cmd/wrapper.go"
    // The ${} placeholder will be rendered in cross-compile
    go "build${debugFlags}${ldflags} -o ./build/\${GOOS}-\${GOARCH}/xl\${GOEXE} cmd/xl/main.go"
    dumpVersion

    finalizedBy ':copyVendor' // so that editors can find the source of dependencies
}

aws {
    profileName = "default"
}

goBuild.dependsOn goTest

goClean.doFirst {
    delete 'build'
}

task copyVendor(type: Copy) {
    from "vendor"
    into ".gogradle/project_gopath/src"
}

task upx {
    dependsOn goBuild
    doLast {
        exec {
            commandLine 'upx', "${project.buildDir}/darwin-amd64/xl"
        }
        exec {
            commandLine 'upx', "${project.buildDir}/linux-amd64/xl"
        }
        exec {
            commandLine 'upx', "${project.buildDir}/windows-amd64/xl.exe"
        }
    }
}

task uploadToS3 {
    mustRunAfter 'publish'
    dependsOn 'uploadDarwinToS3', 'uploadWindowsToS3', 'uploadLinuxToS3'
}

task uploadDarwinToS3(type: AmazonS3FileUploadTask) {
    file file("${project.buildDir}/darwin-amd64/xl")
    bucketName 'xl-cli'
    key "bin/${project.version}/darwin-amd64/xl"
}

task uploadWindowsToS3(type: AmazonS3FileUploadTask) {
    file file("${project.buildDir}/windows-amd64/xl.exe")
    bucketName 'xl-cli'
    key "bin/${project.version}/windows-amd64/xl.exe"
}

task uploadLinuxToS3(type: AmazonS3FileUploadTask) {
    file file("${project.buildDir}/linux-amd64/xl")
    bucketName 'xl-cli'
    key "bin/${project.version}/linux-amd64/xl"
}

task removeLicenseFolder {
    doLast {
        runCmd("rm", "-f", "licenses/*")
    }
}

task downloadLicenses {
    doLast {
        def authToken = project.property('githubOauthToken')
        def goPath = Paths.get("${System.getenv("GOPATH")}/bin/glice").toString()
        runCmd (goPath, "-r", "-f", "-gh", authToken)
    }
}

task moveLicensesToFolder {
    def licenseFiles = "*-license.MD"
    doLast {
        copy {
            from(".") {
                include licenseFiles
            }
            into "licenses"
            delete fileTree(".").matching {
                include licenseFiles
            }
        }
    }
}

task updateLicenses {
    inputs.files "build.gradle"

    dependsOn 'installGlice'
    dependsOn 'removeLicenseFolder'
    dependsOn 'downloadLicenses'
    dependsOn 'moveLicensesToFolder'

    tasks.findByName('removeLicenseFolder').mustRunAfter 'installGlice'
    tasks.findByName('downloadLicenses').mustRunAfter 'removeLicenseFolder'
    tasks.findByName('moveLicensesToFolder').mustRunAfter 'downloadLicenses'
}

goBuild.finalizedBy(updateLicenses)

def runCmd(Object... args) {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine args
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def ldflag(c, v) {
    "-X \"github.com/xebialabs/xl-cli/cmd/xl/cmd.${c}=${v}\" "
}

sonarqube {
    properties {
        property "sonar.projectKey", "xl-cli"
        property "sonar.projectName", "xl-cli"
        property "sonar.sources", "./"
        property "sonar.exclusions", "**/*_test.go,**/vendor/**"
        property "sonar.go.coverage.reportPaths", ".gogradle/reports/coverage/**/*.out"
    }
}